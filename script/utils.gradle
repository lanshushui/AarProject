//判断是否设置了某个插件
static boolean isAndroidPlugin(Project curProject) {
    return curProject.plugins.hasPlugin("com.android.library")
}

static boolean isAppPlugin(Project curProject) {
    return curProject.plugins.hasPlugin("com.android.application")
}

static boolean isJavaPlugin(Project curProject) {
    return curProject.plugins.hasPlugin("java")
}

static String getTaskName(Gradle gradle) {
    return gradle.startParameter.taskRequests[0]?.args.getAt(0) ?: ""
}

static boolean isPublishTask(Gradle gradle) {
    def taskName = getTaskName(gradle)
    def isPublishing = taskName?.contains(":uploadArchives") || taskName?.contains("PublicationToMavenRepository")
    return isPublishing
}

static def execCmd(String cmd, Project project) {
    println("execCmd $cmd")
    def result = new ByteArrayOutputStream()
    project.exec {
        commandLine 'cmd', '/c', cmd
        standardOutput = result
    }.rethrowFailure()
    return result.toString()
}

static def getProjectLastCommitId(String projectPath, Gradle gradle) {
    String cmd = "git log -n 1 --oneline --format=%H ${projectPath.substring(1, projectPath.length())}"
    return execCmd(cmd, gradle.rootProject).trim()
}

//根据commitId 获得commit的信息(不包含作者，时间的)
static def getCommitMsg(String commitId, Gradle gradle) {
    String cmd = "git log --pretty=format:\"%s\" $commitId -1"
    return execCmd(cmd, gradle.rootProject).trim()
}
//进行函数的导出
ext {
    isAndroidPlugin = this.&isAndroidPlugin
    isAppPlugin = this.&isAppPlugin
    isJavaPlugin = this.&isJavaPlugin
    getTaskName = this.&getTaskName
    isPublishTask = this.&isPublishTask
    execCmd = this.&execCmd
    getProjectLastCommitId = this.&getProjectLastCommitId
    getCommitMsg = this.&getCommitMsg
}